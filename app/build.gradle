plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.plugin.serialization)
    alias(libs.plugins.kotlin.compose)
}

android {
    namespace 'com.app.ralaunch'
    compileSdk 36
    ndkVersion "28.0.13004108"

    defaultConfig {
        applicationId "com.app.ralaunch"
        minSdk 25   // <--- ĐỔI TỪ 28 XUỐNG 25 (Android 7.1)
        targetSdk 35
        versionCode 3
        versionName "2.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 只编译 arm64-v8a 架构
        // 使用 splits 配置

        externalNativeBuild {
            cmake {

            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            jniDebuggable true  
        }
        release {
            debuggable false
            minifyEnabled false  
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Release"
                }
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true  // <--- THÊM DÒNG NÀY: Bắt buộc để Android 7 chạy được Java 21 mà không đổi lõi
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        viewBinding true
        prefab true
        compose true
    }

    androidResources {
        noCompress += ['tar.gz', 'tar.xz', 'xz']
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
            pickFirsts += ['lib/arm64-v8a/libc++_shared.so', 'lib/arm64-v8a/libdotnethost.so']
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a'
            universalApk false
        }
    }

    sourceSets {
        main {
            java {
                exclude 'net/dot/android/crypto/**'
            }
        }
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    // <--- THÊM DÒNG NÀY: Thư viện thông dịch Java 21 cho Android 7
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4' 

    // 本地 JAR/AAR 依赖
    implementation files('../app/libs/libSystem.Security.Cryptography.Native.Android.jar')
    implementation files('../external/libs/fmod.jar')
    implementation files('libs/fishnet-release.aar')

    // 项目模块
    implementation project(':shared')

    // 从 ralib 迁移的依赖
    implementation 'com.github.omicronapps:7-Zip-JBinding-4Android:Release-16.02-2.03'
    implementation 'com.google.code.gson:gson:2.13.2'

    // AndroidX 核心库
    implementation libs.core.ktx
    implementation libs.appcompat
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.recyclerview

    // Material Design
    implementation libs.material

    // Kotlin 扩展
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android
    implementation libs.kotlinx.serialization.json

    // Haze (Glassmorphism blur)
    implementation libs.haze
    implementation libs.haze.materials

    // Compose
    implementation platform(libs.compose.bom)
    implementation libs.compose.ui
    implementation libs.compose.ui.graphics
    implementation libs.compose.ui.tooling.preview
    implementation libs.compose.material3
    implementation libs.compose.material.icons
    implementation libs.compose.runtime
    implementation libs.activity.compose
    implementation libs.lifecycle.runtime.compose
    implementation libs.coil.compose
    implementation 'com.github.jeziellago:compose-markdown:0.5.8'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Koin DI
    implementation libs.koin.core
    implementation libs.koin.android
    implementation libs.koin.compose

    // 第三方工具库
    implementation libs.glide
    implementation libs.konfetti
    implementation libs.commons.compress
    implementation libs.xz
    implementation libs.android.svg

    // 测试依赖
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}
