plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.plugin.serialization)
    alias(libs.plugins.kotlin.compose)
}

android {
    namespace 'com.app.ralaunch'
    compileSdk 36
    ndkVersion "28.0.13004108"

    defaultConfig {
        applicationId "com.app.ralaunch"
        minSdk 28
        targetSdk 35
        versionCode 2
        versionName "1.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 只编译 arm64-v8a 架构
        // 使用 splits 配置

        externalNativeBuild {
            cmake {

            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            jniDebuggable true  // 启用 Native 调试
            
         
        }
        release {
            debuggable false
            minifyEnabled false  // TODO: 修复类重复后启用 R8
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            // Release 移除调试符号，减小 native 库体积
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Release"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        viewBinding true
        prefab true
        compose true
    }

    // 禁止 AAPT 压缩这些文件类型，确保 AssetManager 能正确读取
    androidResources {
        noCompress += ['tar.gz', 'tar.xz', 'xz']
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
            // Debug 版本保留符号便于调试，Release 版本移除符号减小体积
            // keepDebugSymbols += ['**/*.so']  // 仅 debug 时启用
            pickFirsts += ['lib/arm64-v8a/libc++_shared.so', 'lib/arm64-v8a/libdotnethost.so']
            
            // 排除运行时动态加载的大型库（改为压缩存储在 assets 中）
            // 这些库在运行时通过 RuntimeLibraryLoader 解压并用 System.load() 加载
            // 总计节省约 90 MB APK 体积
            excludes += [
                'lib/arm64-v8a/libbox64.so',           // 56 MB - Box64
                'lib/arm64-v8a/libmobileglues.so',     // 8 MB - GLES 转换
                'lib/arm64-v8a/libSkiaSharp.so',       // 7 MB - Skia
                'lib/arm64-v8a/libGLESv2_angle.so',    // 5 MB - ANGLE GLES
                'lib/arm64-v8a/libEGL_angle.so',       // 0.3 MB - ANGLE EGL
                'lib/arm64-v8a/libGL_gl4es.so',        // 4 MB - GL4ES
                'lib/arm64-v8a/libEGL_gl4es.so',       // 0.03 MB - GL4ES EGL
                // lib7-Zip-JBinding.so 保留在 APK 中，因为安装游戏时需要立即使用
            ]
        }
    }

    // 临时只针对真机arm64-v8a编译
    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a'
            universalApk false
        }
    }

    // 使用预编译的 System.Security.Cryptography jar，实现官方托管绑定
    sourceSets {
        main {
            java {
                exclude 'net/dot/android/crypto/**'
            }
        }
    }

    kotlinOptions {
        jvmTarget = '21'
    }

    // 关闭 Lint 检查
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    // 本地 JAR/AAR 依赖
    implementation files('../app/libs/libSystem.Security.Cryptography.Native.Android.jar')
    implementation files('../external/libs/fmod.jar')
    implementation files('libs/fishnet-release.aar')

    // 项目模块
    implementation project(':shared')

    // 从 ralib 迁移的依赖
    implementation 'com.github.omicronapps:7-Zip-JBinding-4Android:Release-16.02-2.03'
    implementation 'com.google.code.gson:gson:2.13.2'

    // AndroidX 核心库
    implementation libs.core.ktx
    implementation libs.appcompat
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.recyclerview

    // Material Design
    implementation libs.material

    // Kotlin 扩展
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android
    implementation libs.kotlinx.serialization.json

    // Haze (Glassmorphism blur)
    implementation libs.haze
    implementation libs.haze.materials

    // Compose
    implementation platform(libs.compose.bom)
    implementation libs.compose.ui
    implementation libs.compose.ui.graphics
    implementation libs.compose.ui.tooling.preview
    implementation libs.compose.material3
    implementation libs.compose.material.icons
    implementation libs.compose.runtime
    implementation libs.activity.compose
    implementation libs.lifecycle.runtime.compose
    implementation libs.coil.compose
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Koin DI
    implementation libs.koin.core
    implementation libs.koin.android
    implementation libs.koin.compose

    // 第三方工具库
    implementation libs.glide
    implementation libs.konfetti
    implementation libs.commons.compress
    implementation libs.xz
    implementation libs.android.svg

    // 测试依赖
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}
